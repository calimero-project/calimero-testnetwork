plugins {
    id 'java'
    id 'application'
    id 'eclipse'
    id "com.github.ben-manes.versions" version "0.42.0"
    id 'maven-publish'
    id 'signing'
}

mainClassName = "tuwien.auto.calimero.TestNetwork"

run {
    systemProperties System.getProperties()
    args 'src/main/resources/server-config.xml'
    standardInput = System.in
}

sourceCompatibility = 11
targetCompatibility = 11

compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'
javadoc.options.encoding = 'UTF-8'


group = 'com.github.calimero'
version = '2.6-SNAPSHOT'

repositories {        
    mavenLocal()
    mavenCentral()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
}

compileJava.options*.compilerArgs = [
	'-Xlint:all',
	'-Xlint:-options'
]

java {
    withSourcesJar()
    withJavadocJar()
}

tasks.withType(Jar) {
	from("${projectDir}") {
        include 'LICENSE'
        into "META-INF"
    }
    if (name.equals('sourcesJar')) {
    	from("${projectDir}") {
		    include 'README.md', 'build.gradle', 'settings.gradle', 'gradle*/**'
        }
    }
}

dependencies {
    implementation group:group, name: 'calimero-server', version:version
    implementation group:group, name: 'calimero-core', version:version
    implementation group:group, name: 'calimero-device', version:version
    runtimeOnly 'org.slf4j:slf4j-simple:1.7.30'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = rootProject.name
            from components.java
            pom {
                name = 'Calimero test network'
                description = 'Test network with KNXnet/IP server and virtual subnet'
                url = 'https://github.com/calimero-project/calimero-testnetwork'
                licenses {
                    license {
                        name = "GNU General Public License, version 2, with the Classpath Exception"
                        url = 'LICENSE'
                    }
                }
                developers {
                    developer {
                        name = "Boris Malinowsky"
                        email = "b.malinowsky@gmail.com"
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com:calimero-project/calimero-testnetwork.git'
                    url = 'https://github.com/calimero-project/calimero-testnetwork.git'
                }
            }
        }
    }
    repositories {
        maven {
            name 'maven'
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials(PasswordCredentials)
        }
    }
}

signing {
    if (project.hasProperty("signing.keyId")) {
        sign publishing.publications.mavenJava
    }
}
